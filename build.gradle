plugins {
  id 'io.github.goooler.shadow' version '8.1.8'
  id 'com.diffplug.spotless' version '8.1.0'
  id 'java'
  id 'idea'
}

repositories {
  mavenCentral()
}

sourceSets {
  main {
    java {
      srcDir 'src'
    }
    resources {
      srcDir 'settings'
    }
  }
  test {
    java {
      srcDir 'test'
    }
  }
}

dependencies {
  // logging
  implementation 'org.slf4j:slf4j-api:2.0.17'
  implementation 'ch.qos.logback:logback-classic:1.5.22'
  // config
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.20.1'
  // vertx
  implementation "io.vertx:vertx-web-client:${vertxVersion}"
  implementation "io.vertx:vertx-auth-oauth2:${vertxVersion}"
  implementation "io.vertx:vertx-mongo-client:${vertxVersion}"
  implementation "io.vertx:vertx-web:${vertxVersion}"
  implementation "io.vertx:vertx-rx-java3:${vertxVersion}"
  //lucene
  implementation "org.apache.lucene:lucene-core:${luceneVersion}"
  implementation "org.apache.lucene:lucene-queryparser:${luceneVersion}"
  implementation "org.apache.lucene:lucene-misc:${luceneVersion}"
  implementation "org.apache.lucene:lucene-analysis-common:${luceneVersion}"
  // common libs
  implementation 'org.jsoup:jsoup:1.21.2'
  implementation 'org.apache.commons:commons-collections4:4.5.0'
  implementation 'org.ocpsoft.prettytime:prettytime:5.0.9.Final'
  implementation 'org.javatuples:javatuples:1.2'
  // metrics
  implementation "io.prometheus:prometheus-metrics-core:${prometheusVersion}"
  implementation "io.prometheus:prometheus-metrics-instrumentation-jvm:${prometheusVersion}"
  implementation "io.prometheus:prometheus-metrics-exporter-httpserver:${prometheusVersion}"
  // test
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.14.1'
}

jar {
  manifest {
    attributes 'Main-Class': 'io.github.sammers.pla.Main'
  }
}

test {
  useJUnitPlatform {
  }
}

shadowJar {
  archiveBaseName.set('wow-pla')
  archiveClassifier.set('')
  archiveVersion.set('')
  mergeServiceFiles()
}
java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(25)
  }
}

spotless {
  java {
    target 'src/**/*.java', 'test/**/*.java'
    eclipse().configFile('eclipse-formatter.xml')
    removeUnusedImports()
    trimTrailingWhitespace()
    endWithNewline()
  }
}
